#!/usr/bin/env perl
use 5.014;
use warnings;
use List::Util qw( max sum );

our $VERSION = '1.00';

use Getopt::Long '2.33';
GetOptions(\my %opt,
	'width|w=i',
) or exit 64;  # EX_USAGE
$opt{width} ||= $ENV{COLUMNS} || 80;

my @lines = readline or exit;
chomp for @lines;
my @values = map { s/^\h*([0-9]*)// and $1 } @lines;
my @order  = sort { $b <=> $a } @values;
my $lenval = 1 + int log($order[0]) / log 10;  # max string length
my $len    = 1 + max map { length } @lines;  # left padding
my $size   = ($opt{width} - $lenval - $len) / $order[0];  # bar multiplication

sub orderpos { ($order[$_[0]] + $order[$_[0] + .5]) / 2 * $size }
my @barmark;
$barmark[ sum(@values) / @values * $size ] = '=';  # average
$barmark[ orderpos($#order / 2) ] = '+';  # mean
defined and $_ = "\e[36m$_\e[0m" for @barmark;

for my $nr (0 .. $#lines) {
	my $val = $values[$nr];
	my $color =
		$val == $order[0] ? 32 : # max
		$val == $order[-1] ? 31 : # min
		90;
	printf "\e[%sm", $color if $color;
	printf "%*s", $lenval, $val;
	print "\e[0m" if $color;
	printf '%-*s', $len, $lines[$nr];
	print $barmark[$_] // '-' for 1 .. $val * $size;
	say '';
}

__END__

=head1 NAME

graph - append bar chart to input numbers

=head1 SYNOPSIS

B<graph> [<options>] [<input>]

cat ... | uniq -c | graph

=head1 DESCRIPTION

Each line starting with a number is given a bar to visualise relative sizes.

=head1 OPTIONS

=over

=item -w, --width=<columns>

Override the maximum number of columns to use.
Appended graphics will extend to fill up the entire screen.

=back

=head1 AUTHOR

Mischa POSLAWSKY <perl@shiar.org>

=head1 LICENSE

GPL3+.

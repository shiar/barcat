#!/usr/bin/env perl
use 5.014;
use warnings;
use List::Util qw( max sum );
use open qw( :std :utf8 );

our $VERSION = '1.00';

use Getopt::Long '2.33';
sub podexit { require Pod::Usage; Pod::Usage::pod2usage(-exitval => 0, @_) }
GetOptions(\my %opt,
	'color|c!',
	'width|w=i',
	'usage|h' => sub { podexit() },
	'help'    => sub { podexit(-verbose => 2) },
) or exit 64;  # EX_USAGE
$opt{width} ||= $ENV{COLUMNS} || 80;
$opt{color} //= 1;

my @lines = readline or exit;
chomp for @lines;
my @values = map { s/^\h*(-?[0-9]*)// and $1 } @lines;
my @order  = sort { $b <=> $a } @values;
my $maxval = max $order[0], -$order[-1];
my $lenval = 1 + ($order[-1] < 0) + int log($maxval || 1) / log 10;  # max string length
my $len    = 1 + max map { length } @lines;  # left padding
my $size   = ($opt{width} - $lenval - $len) / $maxval;  # bar multiplication

sub orderpos { ($order[$_[0]] + $order[$_[0] + .5]) / 2 * $size }
my @barmark;
$barmark[ sum(@values) / @values * $size ] = '=';  # average
$barmark[ orderpos($#order / 2) ] = '+';  # mean
defined and $opt{color} and $_ = "\e[36m$_\e[0m" for @barmark;

for my $nr (0 .. $#lines) {
	my $val = $values[$nr];
	my $color = !$opt{color} ? 0 :
		$val == $order[0] ? 32 : # max
		$val == $order[-1] ? 31 : # min
		90;
	printf "\e[%sm", $color if $color;
	printf "%*s", $lenval, $val;
	print "\e[0m" if $color;
	printf '%-*s', $len, $lines[$nr];
	print $barmark[$_] // '-' for 1 .. abs $val * $size;
	say '';
}

__END__

=head1 NAME

graph - append bar chart to input numbers

=head1 SYNOPSIS

B<graph> [<options>] [<input>]

=head1 DESCRIPTION

Each line starting with a number is given a bar to visualise relative sizes.

=head1 OPTIONS

=over

=item --no-color

Disable colored output of values and bar markers.

=item -w, --width=<columns>

Override the maximum number of columns to use.
Appended graphics will extend to fill up the entire screen.

=back

=head1 EXAMPLES

Commonly used after counting, such as users on the current server:

    users | sed 's/ /\n/g' | sort | uniq -c | graph

Letter frequencies in text files:

    cat /usr/share/games/fortunes/*.u8 |
    perl -CO -nE 'say for grep length, split /\PL*/, uc' |
    sort | uniq -c | graph

Memory usage of user processes:

    ps xo %mem,pid,cmd | graph -l40

Sizes (in megabytes) of all root files and directories:

    du -d0 -m * | graph

Number of HTTP requests per day:

    cat log/access.log | cut -d\  -f4 | cut -d: -f1 | uniq -c | graph

Any kind of database query with leading counts:

    echo 'SELECT count(*),schemaname FROM pg_tables GROUP BY 2' |
    psql -t | graph

Git statistics, such commit count by year:

    git log --pretty=%ci | cut -b-4 | uniq -c | graph

Or the most frequent authors:

    git shortlog -sn | graph | head -3

=head1 AUTHOR

Mischa POSLAWSKY <perl@shiar.org>

=head1 LICENSE

GPL3+.

#!/usr/bin/env perl
use 5.014;
use warnings;
use List::Util qw( max );

my $width = $ENV{COLUMNS} || 80;

my @lines = readline;
chomp for @lines;
my @values = map { s/^\h*([0-9]*)// and $1 } @lines;
my $maxval = max @values;
my $lenval = 1 + int log($maxval) / log 10;  # max string length
my $len    = 1 + max map { length } @lines;  # left padding
my $size   = ($width - $lenval - $len) / $maxval;  # bar multiplication

for my $nr (0 .. $#lines) {
	my $val = $values[$nr];
	printf "\e[90m";
	printf "%*s", $lenval, $val;
	print "\e[0m";
	printf '%-*s', $len, $lines[$nr];
	say '-' x ($val * $size);
}

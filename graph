#!/usr/bin/env perl
use 5.014;
use warnings;
use List::Util qw( max );

my $width = $ENV{COLUMNS} || 80;

my @lines = readline;
chomp for @lines;
my @values = map { s/^\h*([0-9]*)// and $1 } @lines;
my @order  = sort { $b <=> $a } @values;
my $lenval = 1 + int log($order[0]) / log 10;  # max string length
my $len    = 1 + max map { length } @lines;  # left padding
my $size   = ($width - $lenval - $len) / $order[0];  # bar multiplication

my @barmark;
my $mean   = ($order[$#order / 2] + $order[$#order / 2 + .5]) / 2;
$barmark[ $mean * $size ] = '+';  # mean

for my $nr (0 .. $#lines) {
	my $val = $values[$nr];
	my $color =
		$val == $order[0] ? 32 : # max
		$val == $order[-1] ? 31 : # min
		90;
	printf "\e[%sm", $color if $color;
	printf "%*s", $lenval, $val;
	print "\e[0m" if $color;
	printf '%-*s', $len, $lines[$nr];
	print $barmark[$_] // '-' for 1 .. $val * $size;
	say '';
}
